plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

group 'com.kazurayam'
version '0.6.0'

ext.isReleaseVersion = ! version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
    mavenLocal()
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }
tasks.withType(GroovyCompile).each {it.groovyOptions.encoding = defaultEncoding }

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

ext {
    ashotVersion = '1.5.4'
}

dependencies {

    implementation group: 'ru.yandex.qatools.ashot', name: 'ashot', version: "${ashotVersion}"
    implementation group: 'com.kazurayam', name: 'kazurayam_FreeMarker_directives', version: "0.2.0"
    implementation group: 'com.kazurayam', name: 'subprocessj', version: '0.1.0'

    implementation group: 'commons-io', name: 'commons-io', version: "2.8.0"
    implementation group: 'io.github.java-diff-utils', name: 'java-diff-utils', version: "4.9"
    implementation group: 'org.freemarker', name: 'freemarker', version: "2.3.31"
    implementation group: 'com.google.code.gson', name: 'gson', version: "2.8.9"
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.10'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.14.3'
    implementation group: 'com.google.guava', name: 'guava', version: '23.0'

    // Selenium jar is NOT required for materialstore
    //implementation "org.seleniumhq.selenium:selenium-java:3.141.59"
    //implementation group: 'io.github.bonigarcia', name: 'webdrivermanager', version: "4.4.3"

    implementation group: 'org.slf4j', name: 'slf4j-api', version: "1.7.31"
    implementation group: 'org.slf4j', name: 'slf4j-api', version: "1.7.31"

    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: "1.7.31"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
}

tasks.withType(GroovyCompile) {
    options.compilerArgs << '-Xlint:unchecked'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

javadoc {
    failOnError false
}

task copyJavadocToDocsDir(type: Copy) {
    from 'build/docs/javadoc'
    into 'docs/api'
}
javadoc.finalizedBy copyJavadocToDocsDir


task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        materialstore(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
            pom {
                groupId = project.group
                name = project.rootProject.name
                description = 'A Java/Groovy API to store objects (Screenshots, HTML, JSON, XML) collected during End-to-end testings empowered by diffing and reporting tools'
                url = 'https://kazurayam.github.io/materialstore/'
                from components.java
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org.licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kazurayam'
                        name = 'URAYAMA,kazuaki'
                        email = 'kazuaki.urayama@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/kazurayam/${project.rootProject.name}.git"
                    developerConnection = "scm:git:git@github.com:kazurayam/${project.rootProject.name}.git"
                    url = "https://github.com/kazurayam/${project.rootProject.name}"
                }
            }
        }
    }
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo: snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
}

signing {
    sign publishing.publications.materialstore
}
// I do not want to sign the SNAPSHOT
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

