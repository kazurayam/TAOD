allprojects {
    group 'com.kazurayam'
    version '0.9.0'
    ext {
        isReleaseVersion = !version.endsWith("SNAPSHOT")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    repositories {
        mavenCentral()
        mavenLocal()
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    def defaultEncoding = 'UTF-8'
    tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }
    tasks.withType(GroovyCompile).each { it.groovyOptions.encoding = defaultEncoding }

    compileJava {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked'
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    task javadocJar(type: Jar) {
        archiveClassifier = 'javadoc'
        from javadoc
    }

    javadoc {
        failOnError false
        options.locale = "en_US"
    }

    task copyJavadocToDocsDir(type: Copy) {
        from 'build/docs/javadoc'
        into 'docs/api'
    }
    javadoc.finalizedBy copyJavadocToDocsDir

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }
    publishing {
        publications {
            materialstore(MavenPublication) {
                artifact sourcesJar
                artifact javadocJar
                pom {
                    groupId = project.group
                    name = project.rootProject.name
                    description = 'A Java/Groovy API to store objects (Screenshots, HTML, JSON, XML) collected during End-to-end testings empowered by diffing and reporting tools'
                    url = 'https://kazurayam.github.io/materialstore/'
                    from components.java
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://www.apache.org.licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'kazurayam'
                            name = 'URAYAMA,kazuaki'
                            email = 'kazuaki.urayama@gmail.com'
                        }
                    }
                    scm {
                        connection = "scm:git:https://github.com/kazurayam/${project.rootProject.name}.git"
                        developerConnection = "scm:git:git@github.com:kazurayam/${project.rootProject.name}.git"
                        url = "https://github.com/kazurayam/${project.rootProject.name}"
                    }
                }
            }
        }
        repositories {
            maven {
                def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = isReleaseVersion ? releaseRepo : snapshotRepo
                credentials {
                    username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                    password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
                }
            }
        }
    }

    signing {
        sign publishing.publications.materialstore
    }

    // I do not want to sign the SNAPSHOT
    tasks.withType(Sign) {
        onlyIf { isReleaseVersion }
    }

    dependencies {

        // Gson is required by com.kazurayam.materialstore.filesystem.TemplateReady
        implementation group: 'com.google.code.gson', name: 'gson', version: "2.8.9"

        // https://mvnrepository.com/artifact/commons-io/commons-io
        implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'

        // httpclient is required by com.kazurayam.materialstore.filesystem/Metadata
        implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'

        // slf4j is required by many classes in this library
        implementation group: 'org.slf4j', name: 'slf4j-api', version: "1.7.31"

        testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: "1.7.31"

        // JUnit Jupiter is used by all unit tests of all subprojects
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    }
}

project("base") {
    dependencies {
        implementation(project(":core"))

        // Subprocessj is used to run "dot" of Graphviz
        // by com.kazurayam.materialstore.util.DotUtil class
        // https://mvnrepository.com/artifact/com.kazurayam/subprocessj
        implementation group: 'com.kazurayam', name: 'subprocessj', version: '0.3.4'

        // WebDriverManager is required by com.kazurayam.materialstore.materialize.MaterializingFunctions
        implementation group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '5.1.1'

        // java-diff-utils is required by com.kazurayam.materialstore.reduce.differ.AbstractTextDiffer
        implementation group: 'io.github.java-diff-utils', name: 'java-diff-utils', version: "4.9"

        // FreeMarker is required by com.kazurayam.materialstore.report.MProductGroupReporterImpl
        implementation group: 'org.freemarker', name: 'freemarker', version: "2.3.31"

        // kazurayam_FreeMarker_directives is also required by the MProductGroupReporterImpl
        implementation group: 'com.kazurayam', name: 'kazurayam_FreeMarker_directives', version: "0.2.0"

        // jsoup is required by com.kazurayam.materialstore.reduce.differ.TextDifferToHTML
        implementation group: 'org.jsoup', name: 'jsoup', version: '1.14.3'

        // selenium-java is required by com.kazurayam.materialstore.materialize.MaterializingFunctions
        implementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'

        // AShot is required by com.kazurayam.materialstore.reduce.differ.ImageDifferToPNG
        implementation group: 'ru.yandex.qatools.ashot', name: 'ashot', version: "1.5.4"
    }
}

project("core") {
    dependencies {

        // Guava is used by com.kazurayam.materialstore.filesystem.TemplateReady
        // https://mvnrepository.com/artifact/com.google.guava/guava
        implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'

        // PlantUML jar is used by com.kazurayam.materialstore.filesystem.MaterialTest, MateriaListTest, MProductTest, MProductGroupTest
        // https://mvnrepository.com/artifact/net.sourceforge.plantuml/plantuml
        testImplementation group: 'net.sourceforge.plantuml', name: 'plantuml', version: '8059'
    }
}

project("mapper") {
    dependencies {
        implementation project(":core")
        implementation project(":base")

        // https://mvnrepository.com/artifact/org.apache.poi/poi-ooxml
        implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '3.17'

        // https://mvnrepository.com/artifact/org.apache.commons/commons-csv
        implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.9.0'

        // https://mvnrepository.com/artifact/net.sf.cssbox/pdf2dom
        implementation group: 'net.sf.cssbox', name: 'pdf2dom', version: '2.0.1'

        // https://mvnrepository.com/artifact/org.apache.pdfbox/pdfbox
        implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.25'

        // https://mvnrepository.com/artifact/org.apache.pdfbox/pdfbox-tools
        implementation group: 'org.apache.pdfbox', name: 'pdfbox-tools', version: '2.0.25'

        // https://mvnrepository.com/artifact/com.rometools/rome
        implementation group: 'com.rometools', name: 'rome', version: '1.18.0'

        // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
        implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.1'
    }

}

project("tutorial") {
    dependencies {

    }
}

def exportedProjects = [
        ":core",
        ":base",
        ":mapper",
        ":tutorial"
]
task javadocAll(type: Javadoc) {
    source exportedProjects.collect {
        project(it).sourceSets.main.allJava
    }
    classpath = files(exportedProjects.collect {
        project(it).sourceSets.main.compileClasspath
    })
    destinationDir = file("${buildDir}/docs/javadoc-all")
}